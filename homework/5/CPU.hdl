// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	//// Answer
    Not(in=instruction[15],out=ni);               // ni = 1 bila A-instruction (bit 15 = 0)

	// Bila A-instruction → gunakan 'outtM' (hasil ALU sebelumnya, tapi aman karena load=0)
	// Bila C-instruction → gunakan 'instruction' (agar A-register bisa ambil constant? Tidak, tetap A-instruction yg load)
	Mux16(a=outtM,b=instruction,sel=ni,out=i);   // i = nilai untuk A-register

	// ------ A REGISTER ------
	Or(a=ni,b=instruction[5],out=intoA);         // load A bila A-instruction atau bila C-instr punya dest ke A (a-bit=instruction[5])
	ARegister(in=i,load=intoA,out=A,out[0..14]=addressM);   // simpan nilai ke A-register

	// ------ SELECT INPUT FOR ALU: A or M ------
	And(a=instruction[15],b=instruction[12],out=AorM);  // sel = 1 bila C-instruction & a-bit=1 → pakai M
	Mux16(a=A,b=inM,sel=AorM,out=AM);             // AM = nilai untuk ALU.y (A-register atau M[address])

	// ------ ALU ------
	ALU(
    	x=D,                   // x = D-register
    	y=AM,                  // y = A atau M
    	zx=instruction[11],    // control bits
    	nx=instruction[10],
    	zy=instruction[9],
    	ny=instruction[8],
    	f=instruction[7],
    	no=instruction[6],
    	out=outtM,             // hasil ALU
    	out=outM,              // output ke Memory.outM
    	zr=zr,                 // zero flag
    	ng=ng                  // negative flag
	);

	// ------ D REGISTER ------
	And(a=instruction[15],b=instruction[4],out=intoD);   // load D bila C-instruction & dest D=1
	DRegister(in=outtM,load=intoD,out=D);

	// ------ WRITE TO MEMORY ------
	And(a=instruction[15],b=instruction[3],out=writeM);  // writeM bila C-instruction & dest M=1

	// ------ JUMP LOGIC (j1 j2 j3 bits) ------

	// j1 (JGT)
	Not(in=ng,out=pos);             // pos = NOT negative flag
	Not(in=zr,out=nzr);             // nzr = NOT zero flag
	And(a=instruction[15],b=instruction[0],out=jgt);     // jgt bit = j1
	And(a=pos,b=nzr,out=posnzr);    // positive non-zero
	And(a=jgt,b=posnzr,out=ld1);    // load PC bila JGT terpenuhi

	// j2 (JEQ)
	And(a=instruction[15],b=instruction[1],out=jeq);     // jeq bit = j2
	And(a=jeq,b=zr,out=ld2);        // bila equal (zr = 1) → jump

	// j3 (JLT)
	And(a=instruction[15],b=instruction[2],out=jlt);     // jlt bit = j3
	And(a=jlt,b=ng,out=ld3);        // bila negative → jump

	// Combine all jump signals
	Or(a=ld1,b=ld2,out=ldt);        // jgt OR jeq
	Or(a=ld3,b=ldt,out=ld);         // jlt OR (jgt/jeq)

	// ------ PROGRAM COUNTER ------
	PC(
    	in=A,               // lompat ke alamat A
    	load=ld,            // load bila jump terpenuhi
    	inc=true,           // selalu increment bila tidak jump
    	reset=reset,
    	out[0..14]=pc
	);
}